plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'com.oxywire'
version = '1.0'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = 'https://repo.citizensnpcs.co/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'net.citizensnpcs:citizensapi:2.0.28-SNAPSHOT'
    implementation 'cloud.commandframework:cloud-paper:1.5.0'
}

shadowJar {
    relocate('cloud.commandframework.cloud.paper', 'com.oxywire.pumpkincarver.libs.cloud')
    relocate('io.leangen.geantyref', 'com.oxywire.pumpkincarver.libs.geantyref')
    minimize()
    archiveFileName.set('PumpkinCarver.jar')
}

jar {
    archiveFileName.set('PumpkinCarver-noshade.jar')
}

build {
    dependsOn shadowJar
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
